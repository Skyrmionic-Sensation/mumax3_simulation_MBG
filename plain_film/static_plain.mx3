t1 := now()
OutputFormat = OVF2_TEXT
NX := 512
NY := 1
NZ := 8
SetGridsize(NX, NY, NZ)
SetCellSize(3.6e-9, 100e-9, 1e-09)
EdgeSmooth = 8

// magnonic waveguide
mc := cuboid(1843.2e-9, 100e-9, 8e-9)

SetGeom(mc)
saveas(geom, "PlainNiFe")

Msat = 800e3
Aex = 1.3e-11
alpha = 0.006

// absorbing boundary layer
alp_lo := 0.006
alp_hi := 1

// power of the polynomial ABL equation
n := 2

// length of a sub-layer
del := 3.6e-9

// length of the ABL
l_abl := 270e-09

// starting co-ordinate of ABLs on right and left side
xstartL := -921.6e-09
xstartR := 651.6e-09

// no. of sublayer in an ABL
n_sub_layer := l_abl/del
print("No. of sublayer in the ABL:", n_sub_layer)

// coefficient
coeff := (alp_hi - alp_lo) / pow(l_abl, n)

for k := 0; k < n_sub_layer; k++ {
	// ABL on left side of the magnonic structure
	x_currL := xstartL + (k * del)
	alp_kL := coeff * pow((k*del - l_abl), n) + alp_lo
	DefRegion(k+1, xrange(x_currL, x_currL+del))
	alpha.setregion(k+1, alp_kL)

	// ABL on right side of the magnonic structure
	x_currR := xstartR + (k * del)
	alp_kR := coeff * pow((k*del), n) + alp_lo
	DefRegion(n_sub_layer+k+1, xrange(x_currR, x_currR+del))
	alpha.setregion(n_sub_layer+k+1, alp_kR)
}

m = Uniform(1, 1, 0)
saveas(m, "uniform")
B := 0.069
ang := 0 * (pi/180)
B_ext = Vector(B*cos(ang), B*sin(ang), 0)
relax()
save(m)
print(since(t1))
